# ç»„æˆåŸç†å®éªŒæŠ¥å‘Š Lab 5

<p align="right">PB18000227 è‰¾è¯­æ™¨</p>

<!-- toc -->

- [æµæ°´çº¿CPU](#æµæ°´çº¿CPU)
	- [å®éªŒç›®æ ‡](#å®éªŒç›®æ ‡)
	- [å®éªŒå†…å®¹](#å®éªŒå†…å®¹)
		- [æµæ°´çº¿CPU](#æµæ°´çº¿CPU)
			- [pipeline_CPU.v](#Pipeline_CPU)
			- [reg_file.v](#Reg_File)
			- [MUX.v](#MUX)
			- [Multi_Mux.v](#ä¸‰ã€å››è·¯é€‰æ‹©å™¨)
			- [ALU.v](#ALU)
			- [simulation](#Simulation)
	- [æ€è€ƒé¢˜](#æ€è€ƒé¢˜)
		- [é™æ€åˆ†æ”¯é¢„æµ‹](#é™æ€åˆ†æ”¯é¢„æµ‹)
		- [åŠ¨æ€åˆ†æ”¯é¢„æµ‹](#åŠ¨æ€åˆ†æ”¯é¢„æµ‹)
		- [åŠ¨æ€åˆ†æ”¯é¢„æµ‹ä»£ç ](#åŠ¨æ€åˆ†æ”¯é¢„æµ‹ä»£ç )
			- [å®Œæ•´çš„CPUä»£ç ](#å®Œæ•´çš„CPUä»£ç )
			- [é¢„æµ‹å•å…ƒ](#é¢„æµ‹å•å…ƒ)
			- [ä¸»è¦çš„ä»£ç åˆ†æ](#ä¸»è¦çš„ä»£ç åˆ†æ)

<!-- tocstop -->

## æµæ°´çº¿CPU

### å®éªŒç›®æ ‡

1. ç†è§£æµæ°´çº¿CPUçš„ç»„æˆç»“æ„å’Œå·¥ä½œåŸç†ï¼›
2. æŒæ¡æ•°å­—ç³»ç»Ÿçš„è®¾è®¡å’Œè°ƒè¯•æ–¹æ³•ï¼›
3. ç†Ÿç»ƒæŒæ¡æ•°æ®é€šè·¯å’Œæ§åˆ¶å™¨çš„è®¾è®¡å’Œæè¿°æ–¹æ³•ã€‚

### å®éªŒå†…å®¹

#### CPUå®ç°ä»£ç 

##### Pipeline_CPU

```verilog
module pipeline_CPU
	#(parameter WIDTH = 32)
	(
		input clk, rst
	);

	localparam [5:0] R_TYPE = 6'b000000;
	localparam [5:0] LW = 6'b100011;
	localparam [5:0] SW = 6'b101011;
	localparam [5:0] BEQ = 6'b000100;
	localparam [5:0] J_TYPE = 6'b000010;
	localparam [2:0] ADDI = 6'b001000;

	reg [WIDTH-1:0] NPC, IR;					// IF/ID inter-stages register
	reg [WIDTH-1:0] A, B, IMM, RS, RT, RD;		// ID/EX inter-stages register
	// WAE for WB_address_from_EX_stage
	reg [WIDTH-1:0] ALUOut, WMD;				// EX/MEM inter-stages register
	reg [4:0] WAE;								// EX/MEM inter-stages register
	// MDR for mem_data_reg, ADR for ALUout_data_reg, WAM for WB_address_from_MEM_stage
	reg [WIDTH-1:0] MDR, ADR;					// MEM/WB inter-stages register
	reg [4:0] WAM;								// MEM/WB inter-stages register
	reg [WIDTH-1:0] PC;

	wire [WIDTH-1:0] npc;						// PC+4, IF stage
	wire [WIDTH-1:0] ir;						// output of Instruction Mem, IF stage
	wire [WIDTH-1:0] shift_left, pc_beq;		// BEQ addr, ID stage
	wire [WIDTH-1:0] jump_addr;					// JUMP addr, ID stage
	wire [WIDTH-1:0] not_jump, PC_final;		// Branch complete, ID stage
	wire [9:0] haz;								// IR for Hazard Detection Unit, ID stage
	wire [5:0] op;								// IR for Control Unit, ID stage
	wire [WIDTH-1:0] extend;					// Sign extended imme, ID stage
	wire [WIDTH-1:0] a, b;						// register file read ports, ID stage
	wire [15:0] beq_forward;					// containing op, rs, rt infro, used in Forwarding Unit, ID stage
	wire [WIDTH-1:0] addr_imme;					// the same as 'extend', EX stage
	wire [4:0] rs, rt, rd;						// ID fragments, EX stage
	wire [5:0] func;							// for ALU Control Unit, EX stage
	wire [WIDTH-1:0] alu_a, alu_b, alu_result;	// ALU input/output, EX stage
	wire ALU_Zero, cf, of, sf;					// for unused ports of ALU, EX stage
	wire [WIDTH-1:0] final_b;
	wire [4:0] rw_addr;							// chosen register write addr, EX stage
	wire [WIDTH-1:0] mem_out;					// MEM_IP out, MEM_stage
	wire [WIDTH-1:0] reg_data;					// WB stage
	wire [4:0] reg_addr;						// WB stage

	// Control Signals, definations & inter-stages
	reg [1:0] ALUOp, ForwardA, ForwardB, ALU_MUX;
	reg [2:0] ALU_Ctrl, ForwardReg;
	reg PCwe;									// the Boss PC Write control, IF stage
	reg RegDst, AluSrc, MemRead, MemWrite, MemtoReg, RegWrite;
	reg Branch, Jump;
	reg PCWrite, FDWrite, ControlFlush, IFFlush;
	reg [1:0] ALUOp_DE;
	reg AluSrc_DE, RegDst_DE, MemRead_DE, MemWrite_DE, MemtoReg_DE, RegWrite_DE;
	reg MemRead_EM, MemWrite_EM, MemtoReg_EM, RegWrite_EM;
	reg MemtoReg_MW, RegWrite_MW;

	// Control Signals, in use, assigned from stage_regs
	wire Zero, PCSrc;

	assign op = IR[31:26];
	assign haz = IR[25:16];
	assign extend = {((IR[15]) ? 16'hffff : 16'h0000), IR[15:0]};
	assign beq_forward = IR[31:16];
	assign rs = RS;
	assign rt = RT;
	assign rd = RD;
	assign addr_imme = IMM;
	assign reg_addr = WAM;

	// IF
	always @(posedge clk) begin
		if (rst) begin
			PC <= 0;
		end
		else if (PCwe) begin
			PC <= PC_final;
		end
		else
			PC <= PC;
	end

	assign npc = PC + 4;

	mux MUX_BEQ (
		.m(PCSrc),
		.in_0(npc),
		.in_1(pc_beq),
		.out(not_jump)
	);

	mux MUX_JUMP (
		.m(Jump),
		.in_0(not_jump),
		.in_1(jump_addr),
		.out(PC_final)
	);

	dist_inst_rom instruction (
		.a(PC[9:2]),
		.spo(ir)
	);

	// IF/ID inter-stages registers
	always @(posedge clk) begin
		if (IFFlush) begin
			NPC <= 0;
			IR <= 0;
		end
		else begin
			if (FDWrite) begin
				NPC <= npc;
				IR <= ir;
			end
			else begin
				NPC <= NPC;
				IR <= IR;
			end
		end
	end

	// ID
	reg_file registers (
		.clk(clk),
		.forward(ForwardReg),
		.ex(alu_result),
		.mem(mem_out),
		.ra0(IR[25:21]),
		.ra1(IR[20:16]),
		.rd0(a),
		.rd1(b),
		.wa(reg_addr),
		.wd(reg_data),
		.we(RegWrite_MW)
	);

	// for BEQ and Jump
	assign Zero = (a == b) ? 1'b1 : 1'b0;
	assign shift_left = extend << 2;
	assign pc_beq = shift_left + NPC;
	assign jump_addr = {NPC[31:28], IR[25:0], 2'b00};

	// Control Unit
	always @(posedge clk) begin
		if (rst) begin
			{RegDst, Jump, Branch, MemRead, MemtoReg, RegWrite, MemWrite, ALUOp, IFFlush, AluSrc, ForwardReg} = 0;
		end
	end

	always @(*) begin
		{RegDst, Jump, Branch, MemRead, MemtoReg, RegWrite, MemWrite, ALUOp, AluSrc} = 0;
		case (op)
			6'b000000: begin					// R-type
				RegDst = 1'b1;
				RegWrite = 1'b1;
				ALUOp = 2'b10;
			end
			6'b100011: begin					// lw
				AluSrc = 1'b1;
				MemtoReg = 1'b1;
				RegWrite = 1'b1;
				MemRead = 1'b1;
			end
			6'b101011: begin					// sw
				AluSrc = 1'b1;
				MemWrite = 1'b1;
			end
			6'b000100: begin					// beq
				Branch = 1'b1;
				ALUOp = 2'b01;
			end
			6'b001000: begin					// addi
				AluSrc = 1'b1;
				RegWrite = 1'b1;
			end
			6'b000010: begin					// Jump
				Jump = 1'b1;
			end
			default: {RegDst, Jump, Branch, MemRead, MemtoReg, RegWrite, MemWrite, ALUOp, AluSrc} = 'dz;
		endcase
	end

	// Hazard Detection Unit
	// consider R-type, BEQ, BNE, SW at ID
	// Priority considered
	assign PCSrc = Zero & Branch;
	always @(*) begin
		// no need to stall when BEQ is right after a non-mem-visiting instruction,
		// since forwarding from EX result to ID is done (might decrease clk rate)
		if ((op == BEQ) && (MemRead_DE && ((rt == haz[4:0]) || (rt == haz[9:5])))) begin
			// LW + BEQ hazard, BEQ at ID, stall 1 cycle
			PCWrite = 0;
			PCwe = 0;
			IFFlush = 0;
			FDWrite = 0;
			ControlFlush = 1;
		end
		// Jump or Branch success, clear PC and IF/ID inter-stages registers
		else if (Jump || PCSrc) begin
			PCWrite = 1;						// low priority
			PCwe = 1;
			IFFlush = 1;
			FDWrite = 0;						// low priority, 0 or 1 both are ok
			ControlFlush = 0;					// since BEQ/JUMP do not have latter stages, 0 or 1 both are ok
		end
		// R-type or SW need to be considered since they need rt as operation source
		else if (MemRead_DE && (((rt == haz[4:0]) && ((op == 6'b00000) || (op == 6'b101011))) || (rt == haz[9:5]))) begin
			PCWrite = 0;
			PCwe = 0;
			IFFlush = 0;
			FDWrite = 0;
			ControlFlush = 1;
		end
		else begin
			PCWrite = 1;
			PCwe = 1;
			IFFlush = 0;
			FDWrite = 1;
			ControlFlush = 0;
		end
	end

	// ID/EX inter-stages registers
	// in order to avoid possible data impact, I put all data to 'dz if this reg_pile is set clear
	always @(posedge clk) begin
		if (~IFFlush && ~ControlFlush) begin
			A <= a;
			B <= b;
			RS <= IR[25:21];
			RT <= IR[20:16];
			RD <= IR[15:11];
			RegDst_DE <= RegDst;
			ALUOp_DE <= ALUOp;
			MemRead_DE <= MemRead;
			MemtoReg_DE <= MemtoReg;
			MemWrite_DE <= MemWrite;
			RegWrite_DE <= RegWrite;
			AluSrc_DE <= AluSrc;
			IMM <= extend;
		end
		else begin
			{A, B, RS, RT, RD, IMM} <= 'dz;
			{RegDst_DE, MemRead_DE, MemtoReg_DE, RegWrite_DE, MemWrite_DE, ALUOp_DE, AluSrc_DE} <= 0;
		end
	end

	// EX
	multi_mux MUX_ALU_A (
		.n(2'd2),
		.m(ForwardA),
		.in_0(A),
		.in_1(reg_data),
		.in_2(ALUOut),
		.out(alu_a)
	);

	multi_mux MUX_FINAL_B (
		.n(2'd2),
		.m(ForwardB),
		.in_0(B),
		.in_1(reg_data),
		.in_2(ALUOut),
		.out(final_b)
	);

	assign addr_imme = IMM;
	assign func = IMM[5:0];

	mux MUX_ALU_B (
		.m(AluSrc_DE),
		.in_0(final_b),
		.in_1(IMM),
		.out(alu_b)
	);

	// ALU Control Unit
	always @(*) begin
		case (ALUOp_DE)
			2'b00: begin				// LW & SW
				ALU_Ctrl = 3'b010;
			end
			2'b01: begin				// BEQ
				ALU_Ctrl = 3'b110;
			end
			2'b10: begin				// R-type
				case (func)
					6'b100000: begin	// add
						ALU_Ctrl = 3'b010;
					end
					6'b100010: begin	// sub
						ALU_Ctrl = 3'b110;
					end
					6'b100100: begin	// and
						ALU_Ctrl = 3'b000;
					end
					6'b100101: begin	// or
						ALU_Ctrl = 3'b001;
					end
					6'b101010: begin	// slt
						ALU_Ctrl = 3'b111;
					end
					default: ALU_Ctrl = 'dz;
				endcase
			end
			default: ALU_Ctrl = 'dz;
		endcase
	end

	ALU alu (
		.y(alu_result),
		.zf(ALU_Zero),
		.cf(cf),
		.of(of),
		.sf(sf),
		.a(alu_a),
		.b(alu_b),
		.m(ALU_Ctrl)
	);

	mux MUX_REGDST (
		.m(RegDst_DE),
		.in_0(rt),
		.in_1(rd),
		.out(rw_addr)
	);

	// Forwarding Unit
	// NOT solved WB-start to MEM-start
	// solved WB-start or MEM-start to EX-start
	// For ForwardReg: MEM-start (EX result) or MEM-end (MEM result) to ID
	// 000 for non-forwarding, 001 from ALU to A, 010 from ALU to B,
	// 011 from MEM to A, 100 from MEM to B
	always @(*) begin
		// forward to EX-start
		if (RegWrite_EM && (|WAE) && (WAE == rs)) begin
			ForwardA = 2'b10;			
		end
		else if (RegWrite_MW && (|WAM) && (WAM == rs)) begin
			ForwardA = 2'b01;
		end
		else begin
			ForwardA = 2'b00;
		end
	end

	always @(*) begin
		// forward to EX-start
		if (RegWrite_EM && (|WAE) && (WAE == rt)) begin
			ForwardB = 2'b10;
		end
		else if (RegWrite_MW && (|WAM) && (WAM == rt)) begin
			ForwardB = 2'b01;
		end
		else begin
			ForwardB = 2'b00;
		end
	end

	always @(*) begin
		// current instruction in ID is a BEQ
		if (beq_forward[15:10] == 6'b000100) begin
			if (RegWrite_DE && (|rw_addr) && (rw_addr == beq_forward[9:5])) begin
				ForwardReg = 3'b001;
			end
			else if (RegWrite_DE && (|rw_addr) && (rw_addr == beq_forward[4:0])) begin
				ForwardReg = 3'b010;
			end
			else if (RegWrite_EM && (|WAE) && (WAE == beq_forward[9:5])) begin
				ForwardReg = 3'b011;
			end
			else if (RegWrite_EM && (|WAE) && (WAE == beq_forward[4:0])) begin
				ForwardReg = 3'b100;
			end
			else begin
				ForwardReg = 3'b000;
			end
		end
		else begin
			ForwardReg = 3'b000;
		end
	end

	// EX/MEM inter-stages registers
	always @(posedge clk) begin
		ALUOut <= alu_result;
		WMD <= final_b;
		WAE <= rw_addr;
		MemRead_EM <= MemRead_DE;
		MemWrite_EM <= MemWrite_DE;
		MemtoReg_EM <= MemtoReg_DE;
		RegWrite_EM <= RegWrite_DE;
	end

	// MEM
	dist_data_ram memory (
		.a(ALUOut[9:2]),
		.d(WMD),
		.clk(clk),
		.we(MemWrite_EM),
		.spo(mem_out)
	);

	// MEM/WB inter-stages registers
	always @(posedge clk) begin
		MDR <= mem_out;
		ADR <= ALUOut;
		WAM <= WAE;
		MemtoReg_MW <= MemtoReg_EM;
		RegWrite_MW <= RegWrite_EM;
	end

	// WB
	mux MUX_WB (
		.m(MemtoReg_MW),
		.in_0(ADR),
		.in_1(MDR),
		.out(reg_data)
	);
endmodule
```

##### Reg_File

```verilog
// Containing inner-forwarding
module reg_file
	#(parameter WIDTH = 32)
	(
		input clk,
		input [2:0] forward,		// 000 for non-forwarding, 001 from ALU to A, 010 from ALU to B, 011 from MEM to A, 100 from MEM to B
		input [WIDTH-1:0] ex,		// input data from ALU-result
		input [WIDTH-1:0] mem,		// input data from mem-visit-result
		input [4:0] ra0,			// read port 0 addr
		output reg [WIDTH-1:0] rd0,	// read port 0 data
		input [4:0] ra1,			// read port 1 addr
		output reg [WIDTH-1:0] rd1,	// read port 1 data
		input [4:0] wa,				// write port addr
		input we,					// write enable, valid at '1'
		input [WIDTH-1:0] wd		// write port data
	);

	reg [WIDTH-1:0] reg_file [0:31];

	always @(*) begin
		case (forward)
			// not outside forwarding, consider inner-register forwarding (WB->ID)
			3'b000: begin
				if ((wa == ra0) && (|wa) && we) begin
					rd0 = wd;
					rd1 = reg_file[ra1];
				end
				else if ((wa == ra1) && (|wa) && we) begin
					rd0 = reg_file[ra0];
					rd1 = wd;
				end
				else begin
					rd0 = reg_file[ra0];
					rd1 = reg_file[ra1];
				end
			end
			// forward from EX, since a ALU result is needed
			3'b001: begin
				rd0 = ex;
				if ((wa == ra1) && (|wa) && we) begin
					rd1 = wd;
				end
				else begin
					rd1 = reg_file[ra1];
				end
			end
			3'b010: begin
				if ((wa == ra0) && (|wa) && we) begin
					rd0 = wd;
				end
				else begin
					rd0 = reg_file[ra0];
				end
				rd1 = ex;
			end
			3'b011: begin
				rd0 = mem;
				if ((wa == ra1) && (|wa) && we) begin
					rd1 = wd;
				end else begin
					rd1 = reg_file[ra1];
				end
			end
			3'b100: begin
				if ((wa == ra0) && (|wa) && we) begin
					rd0 = wd;
				end else begin
					rd0 = reg_file[ra0];
				end
				rd1 = mem;
			end
			default: begin
				rd0 = 'dz;
				rd1 = 'dz;
			end
		endcase
	end

	integer i;						// loop varible
	initial begin
		for (i = 0; i < 32; i = i + 1) begin
			reg_file [i] = 0;
		end
	end

	always @(posedge clk) begin
		if (we && wa != 4'b0) begin
			reg_file[wa] <= wd;
		end
	end
endmodule
```

##### MUX

```verilog
module mux
#(parameter WIDTH = 32)
(
	input m, // control signal
	input [WIDTH-1:0] in_0,in_1,
	output [WIDTH-1:0] out
);
	assign out = (m == 1'b0 ? in_0 : in_1);

endmodule // mux
```

##### ä¸‰ã€å››è·¯é€‰æ‹©å™¨

```verilog
module multi_mux
	#(parameter WIDTH = 32)
	(
		input [1:0] n,			// select depth, from 0~3
		input [1:0] m,			// control signal
		input [WIDTH-1:0] in_0, in_1, in_2, in_3,
		output reg [WIDTH-1:0] out
	);

	always @(*) begin
		case (m)
			2'b00: out = in_0;
			2'b01: out = in_1;
			2'b10: out = in_2;
			2'b11: out = (n == 2'd2) ? 'dz : in_3;
			default: out = 'dz;
		endcase
	end
endmodule
```

##### ALU

```verilog
// on basis of the COD 5th edition, we use the following parameters
module ALU
    #(parameter WIDTH = 32) 	// data width
(
    output reg [WIDTH-1:0] y,   // calculation result
    output reg zf,              // zero sign
    output reg cf,              // jinwei sign
	output reg of,				// yichu
	output reg sf,				// for signed cal
	input [WIDTH-1:0] a,
	input [WIDTH-1:0] b,
	input [2:0] m				// type
);

	localparam ADD = 3'b010;
    localparam SUBTRACT = 3'b110;
    localparam AND = 3'b000;
    localparam OR = 3'b001;
    localparam XOR = 3'b100;
    localparam SLT = 3'b111;

    always @(*) begin
		{zf,of,cf,sf} = 4'b0000;
		case (m)
			ADD:  begin
				{cf, y} = a + b;
				of = (~a[WIDTH-1] & ~b[WIDTH-1] & y[WIDTH-1]) | (a[WIDTH-1] & b[WIDTH-1] & ~y[WIDTH-1]);
				zf = ~|y;
			end
			SUBTRACT: begin
				{cf, y} = a - b;
				of = (~a[WIDTH-1] & b[WIDTH-1] & y[WIDTH-1]) | (a[WIDTH-1] & ~b[WIDTH-1] & ~y[WIDTH-1]);
				zf = ~|y;
				sf = y[WIDTH-1];
			end
			AND: begin
				y = a & b;
				zf = ~|y;
				sf = y[WIDTH-1];
			end
			OR: begin
				y = a | b;
				zf = ~|y;
				sf = y[WIDTH-1];
			end
			XOR: begin
				y = a ^ b;
				zf = ~|y;
				sf = y[WIDTH-1];
			end
            SLT: begin
                y = (a < b) ? 32'b1 : 32'b0;
            end
			default: y = a;
		endcase
	end
endmodule
```

##### Simulation

![simulation](pictures/simulation.png)

```verilog
module tb_CPU();
    reg clk, rst;

    pipeline_CPU cpu (
        .clk(clk),
        .rst(rst)
    );

    initial
    begin
        clk = 1;
        rst = 1;
        # 10 rst = 0;
        # 490 $finish;
    end

    always
    # 5 clk = ~clk;
endmodule
```

#### CPUä»£ç åˆ†æ

copyè‡ªæˆ‘è‡ªå·±çš„GitHubå®éªŒä»£ç çš„[README](https://github.com/Lapland-Stark/COD2020/tree/master/lab_files/lab5)

##### æ•°æ®é€šè·¯

![data_path](pictures/pipeline_CPU.png)

##### ç›¸å…³ï¼ˆhazardï¼Œå†’é™©ï¼›dependenciesï¼Œä¾èµ–ï¼‰

- ç»“æ„ç›¸å…³:å½“æŒ‡ä»¤åœ¨é‡å æ‰§è¡Œçš„è¿‡ç¨‹ä¸­ï¼Œç¡¬ä»¶èµ„æºæ»¡è¶³ä¸äº†æŒ‡ä»¤é‡å æ‰§è¡Œçš„è¦æ±‚ï¼Œå‘ç”Ÿèµ„æºå†²çª æ—¶å°†äº§ç”Ÿç»“æ„ç›¸å…³
	- è¿™ä¸ªå¥½åŠï¼Œ~~å……é’±å¯ä»¥è§£å†³ä¸€åˆ‡é—®é¢˜~~ï¼Œå¢åŠ ç›¸åº”çš„éƒ¨ä»¶å°±å¥½äº†ï¼ˆæ¯”å¦‚å“ˆä½›ç»“æ„ï¼‰
- æ•°æ®ç›¸å…³:å½“ä¸€æ¡æŒ‡ä»¤éœ€è¦ç”¨åˆ°å‰é¢æŒ‡ä»¤çš„æ‰§è¡Œç»“æœï¼Œè€Œè¿™äº›æŒ‡ä»¤å‡åœ¨æµæ°´çº¿ä¸­é‡å æ‰§è¡Œæ—¶ï¼Œå°± å¯èƒ½å¼•èµ·æ•°æ®ç›¸å…³
	- ç¼–è¯‘æŠ€æœ¯:æ’å…¥nopï¼ŒæŒ‡ä»¤é‡æ’ï¼Œå¯„å­˜å™¨é‡å‘½
	- forwardingæŠ€æœ¯
	- InterlockæŠ€æœ¯
- æ§åˆ¶ç›¸å…³:å½“æµæ°´çº¿é‡åˆ°åˆ†æ”¯æŒ‡ä»¤å’Œå…¶ä»–ä¼šæ”¹å˜ PCå€¼çš„æŒ‡ä»¤æ—¶ï¼Œä¼šå‘ç”Ÿæ§åˆ¶ç›¸å…³
	- ç¼–è¯‘æŠ€æœ¯:å»¶è¿Ÿåˆ†æ”¯
	- ç¡¬ä»¶ä¼˜åŒ–:æå‰å®Œæˆï¼ŒæŠ•æœºï¼Œé¢„æµ‹

---

###### ä¸ºä»€ä¹ˆè¦æœ‰forwarding / bypassingï¼Ÿ

ä¸ºäº†è®©ä¸€äº›æŒ‡ä»¤ï¼ˆæ¯”å¦‚`add`çš„**WB**æ®µï¼‰ä¸å’Œä¹‹åæŒ‡ä»¤äº§ç”Ÿæ•°æ®ç›¸å…³ï¼ˆRAWï¼‰ã€‚ç›´ç™½æ¥è¯´å°±æ˜¯è®©æŒ‡ä»¤**çœŸæ­£ç”¨åˆ°æ•°æ®**çš„åœ°æ–¹ç”¨åˆ°æ­£ç¡®çš„æ•°æ®

- RAWçš„åˆ¤æ–­è§„åˆ™ï¼š
	- EXï¼šEX/MEM.RegisterRd=ID/EX.RegisterRs
	- EXï¼šEX/MEM.RegisterRd=ID/EX.RegisterRt
	- MEMï¼šMEM/WB.RegisterRd=ID/EX.RegisterRs
	- MEMï¼šMEM/WB.RegisterRd=ID/EX.RegisterRt

ä¸ä¼šå‡ºé”™çš„æ•°æ®æ˜¯å¯„å­˜å™¨çš„ç¼–å·ï¼ˆè¿™éƒ½å†™åœ¨æŒ‡ä»¤é‡Œé¢äº†ï¼‰

ä¼šå‡ºé”™çš„æœ‰**R-type**çš„è¿ç®—ç»“æœ (EX)ï¼Œè®¿å­˜æŒ‡ä»¤çš„åœ°å€ (MEM)

###### Interlock

æœ‰çš„æŒ‡ä»¤å†²çªä¾ç„¶æ— æ³•é€šè¿‡æ—è·¯æ¥è§£å†³ï¼Œæ•…å°†æµæ°´çº¿æ•°æ®ä¾èµ–ä½ç½®ä¹‹å‰çš„æ®µå†»ç»“ï¼Œå¹¶åœ¨åé¢çš„æ®µæ’å…¥æ°”æ³¡

- `if (IF/ID.MemRead && ((ID/EX.RegisterRt == IF/ID.RegisterRs) || (ID/EX.RegisterRt == IF/ID.RegisterRt)))`
- å¢åŠ ä¸¤ä¸ªæ§åˆ¶ä¿¡å·ï¼šPCWrite å’Œ IF/IDWrite
	- é˜»æ­¢æ›´æ–° PC å’Œ IF/IDï¼Œä½¿ä¹‹è¿›è¡Œç›¸åŒçš„æ“ä½œï¼ˆä¿æŒæ­£ç¡®çš„è¯‘ç ç»“æœï¼‰
	- ä¸ºäº†é¿å…åé¢æ®µé‡å¤æ‰§è¡Œ
- å°† ID/EX çš„æ§åˆ¶ä¿¡å·æ¸…é›¶ï¼Œå³ EX æ®µæš‚åœä¸€ä¸ªå‘¨æœŸ

###### å…³äºHazard detection unit

ä½œç”¨è§ä¸Šé¢çš„interlock

æˆ‘å·²çŸ¥çš„ä¸‰ç§è§£æ³•ï¼šBEQåœ¨EXæ®µç»“æŸï¼›BEQåœ¨IDç»“æŸï¼Œä¸¤æ¬¡nopï¼›BEQåœ¨IDç»“æŸï¼Œä¸€æ¬¡nopï¼Œå®Œå…¨è½¬å‘

è¿™ä¸‰ç§è§£æ³•å„è‡ªçš„ä¼˜åŠ£ï¼š

- BEQåœ¨EXæ®µç»“æŸï¼šå®ç°ç®€å•ï¼Œä½†æ˜¯BEQä¼šå¤šä¸€ä¸ªå‘¨æœŸç»“æŸ
- BEQåœ¨IDç»“æŸï¼Œä¸¤æ¬¡nopï¼šç¡¬ä»¶ç®€å•ï¼Œéœ€è¦ç”¨çŠ¶æ€æœºå®ç°hazardå•å…ƒ (nopå’Œä¸nopä¸¤ä¸ªçŠ¶æ€)
- BEQåœ¨IDç»“æŸï¼Œä¸€æ¬¡nopï¼Œå®Œå…¨è½¬å‘ï¼šç›¸å¯¹æ¯”è¾ƒå®Œå…¨çš„è½¬å‘ï¼Œä½†æ˜¯è¿çº¿æ¯”è¾ƒéº»çƒ¦ï¼Œè¿˜éœ€è¦ä¿®æ”¹å¯„å­˜å™¨å †

<font color=red>CAUTION !!!</font>

<font color=coral>ä¸‹é¢CODé‡Œé¢çš„è¿™ä¸ªåªé€‚ç”¨äºBEQåœ¨EXæ®µç»“æŸæ—¶è·³è½¬</font>

- è¿™ä¸ªåˆ¤æ–­æ¡ä»¶åŸç†å¦‚è¿™ä¸ªå›¾æ‰€ç¤º
- ![SW-R_type](pictures/LW_R_type.png)
- è®¿å­˜ç›®æ ‡å¯„å­˜å™¨æ°å¥½åœ¨ä¸‹ä¸€æ¡æŒ‡ä»¤çš„EXæ®µéœ€è¦ç”¨åˆ°ï¼Œä½†MEMæ®µ (è€Œä¸æ˜¯åƒç®—æœ¯æŒ‡ä»¤é‚£æ ·EX) æ‰äº§ç”Ÿç»“æœï¼Œæ•…åªå¾—ğŸ’­
- `if (ID/EX.MemRead and
	 ((ID/EX.RegisterRt = IF/ID.RegisterRs) or
	 (ID/EX.RegisterRt = IF/ID.RegisterRt))) stall the pipeline` 
- (ä¸æ˜¯BEQ / BNE / *SW*) çš„`I`ç±»å‹æŒ‡ä»¤å’Œ`J`ç±»å‹æŒ‡ä»¤æ— é¡»è€ƒè™‘ `ID/EX.RegisterRt = IF/ID.RegisterRt` è¿™ä¸ªæ¡ä»¶ (å› ä¸ºrtæ“ä½œæ•°æ˜¯å†™å›ç›®æ ‡å¯„å­˜å™¨)

å¦‚æœBEQåœ¨IDæ®µå°±æœ‰å¯èƒ½éœ€è¦nopğŸ’­ä¸¤ä¸ªå‘¨æœŸ (LW+*BEQ*)

- åœ¨ä¸Šé¢çš„åŸºç¡€ä¸Šéœ€è¦æŠŠBEQã€BNEæŒ‡ä»¤nopä¸¤ä¸ªå‘¨æœŸ (æˆ–è€…å°†bypassingæ‹“å±•åˆ°IDæ®µå»ï¼Œå¹¶ğŸ’­ä¸€ä¸ªå‘¨æœŸ)
- è¿™ä¸ªæˆ‘å®åœ¨è§£å†³ä¸äº†äº†ï¼Œæˆ‘æ²¡æœ‰åŠæ³•è®©å·¦è¾¹é‚£ä¸ªå¯„å­˜å™¨å †ç­‰ä¸€æ•´ä¸ªmemæ—¶é—´å†è¯»è¿›å»å¹¶æŠŠå€¼ä¼ é€’ç»™beqã€‚ã€‚ã€‚ä¸è¿‡æˆ‘è§‰å¾—å§æ—¶é’Ÿå‘¨æœŸé‚£ä¹ˆé•¿ï¼Œè¿˜æ˜¯å¯ä»¥æ¥å—çš„ (äº‹å®è¯æ˜å¯ä»¥)
- ã€æ³¨ã€‘SWä¹Ÿå¯ä»¥ä¸ğŸ’­ï¼šåŠ ä¸€ä¸ªä»WBåˆ°MEMçš„forwardingå°±å¯ä»¥äº†ã€‚ã€‚

##### æ§åˆ¶ä¿¡å·

###### å…³äºPCwe

PCenåŒæ—¶å— rstã€ jump | zero & branch (æ¥æºäºè·³è½¬æŒ‡ä»¤) å’Œ PCWrite (æ¥æºäºå†’é™©å¤„ç†) æ§åˆ¶

å…¶ä¸­è·³è½¬çš„ä¼˜å…ˆçº§é«˜äºPCwrite (nopé”å­˜æ˜¯ä¸ºäº†æ‰§è¡Œæ¥ä¸‹æ¥çš„æŒ‡ä»¤ï¼Œéƒ½è¦è·³è½¬äº†ä¸ºå•¥ä¸åˆ·æ–°PCå‘¢ã€‚ã€‚)

æ€»ä¹‹ï¼šå¤„ç†BEQå–æ•°å¯„å­˜å™¨æ•°æ®å†²çª (LW+*BEQ*) > è·³è½¬æ‰§è¡Œ > å¤„ç†EXéœ€è¦ç”¨åˆ°æ•°æ®çš„æ•°æ®å†²çª > æ™®é€šPC+4

###### å…³äºID/EXçš„flush

Qï¼šå°½ç®¡æ¸…0äº†å®ƒä¹Ÿéƒ½æ˜¯æ§åˆ¶ä¿¡å·é¸­Â·Â·Â·ä¸ä¼šç»§ç»­æ‰§è¡Œäº§ç”Ÿä»€ä¹ˆå½±å“å˜›ï¼Ÿ

Aï¼šé¦–å…ˆæ‰€æœ‰å†™å…¥ä½¿èƒ½éƒ½æ˜¯0ï¼Œæ‰€ä»¥ä¸ä¼šä¿®æ”¹å­˜å‚¨ä¸‹æ¥çš„æ•°æ®ï¼›å…¶æ¬¡ID/EX.MemReadæ˜¯0ï¼Œåˆ™hazardæ§åˆ¶å•å…ƒä¸å·¥ä½œäº§ç”Ÿå½±å“ï¼›å†æ¬¡RegWriteä¿¡å·éƒ½æ˜¯0ï¼Œæ‰€ä»¥forwardingå•å…ƒä¹Ÿä¸å·¥ä½œÂ·Â·Â·

ä¸è¿‡æ®µé—´å¯„å­˜å™¨é‡Œé¢çš„æ•°æ®æœ€å¥½æŒ‚é«˜é˜»æ€ï¼Œå› ä¸ºforwardingå•å…ƒéœ€è¦ç”¨é‡Œé¢çš„æ•°æ®åšåˆ¤æ–­

###### å…³äºIF.Flush

æƒ³ä¸€ä¸‹å»¶è¿Ÿæ§½ (å°±æ˜¯è½¬ç§»æŒ‡ä»¤åé¢åˆ†æ”¯é¢„æµ‹çš„æ—¶å€™é¢„æ‰§è¡Œçš„é‚£ä¸€éƒ¨åˆ†)ï¼Œè¯»è¿›å»çš„æŒ‡ä»¤ä¼šåœ¨é¢„æµ‹å¤±è´¥ä¹‹åç»§ç»­æ‰§è¡Œï¼Œæ‰€ä»¥éœ€è¦æŠŠå®ƒè¸¢å‡ºå»

åŒæ—¶æˆ‘è®²è¿™ä¸ªä¿¡å·ä¹Ÿç”¨äºåœ¨é¢„æµ‹å¤±è´¥æ—¶æ¸…ç©ºID/EXæ®µé—´å¯„å­˜å™¨ï¼Œä»¥å…è½¬ç§»æŒ‡ä»¤å‰©ä½™çš„æ•°æ®åœ¨åç»­å¯¹Forwardingäº§ç”Ÿå½±å“

##### æŒ‡ä»¤

###### å…³äºBEQ

BEQæŒ‡ä»¤å¯ä»¥ç”¨ 3ä¸ªå‘¨æœŸï¼Œæˆ–è€…2ä¸ªå‘¨æœŸã€‚ä¸¤ä¸ªå‘¨æœŸçš„å¥½å¤„å½“ç„¶æ˜¯å»¶è¿Ÿæ§½å˜æµ…äº†ï¼Œä½†æ˜¯å¯¹äºç›¸å…³çš„å¤„ç†ä¼šå‡ºç°ä¸€äº›é—®é¢˜ã€‚ã€‚

è€ƒè™‘ä¸€ä¸‹è¿™ä¸ªå†’é™©ï¼š

![BEQ_hazard](pictures/BEQ_hazard.png)

å½“BEQæŒ‡ä»¤çš„ä¸€ä¸ªæ•°æ®æ¥æºå¯„å­˜å™¨æ˜¯ä¸Šä¸€æ¡æŒ‡ä»¤çš„å†™å›ç›®æ ‡å¯„å­˜å™¨ï¼Œå°±ã€‚ã€‚ã€‚

##### è®¿å­˜

æ²¡æœ‰å†™æ•°æ®çš„portÂ·Â·Â·è‡ªå·±ä»ID/EXæ®µé—´å¯„å­˜å™¨å¼•Bå‡ºæ¥

##### å…³äºALUçš„Bæ“ä½œæ•°

ForwardB è¿™ä¸ªæ§åˆ¶ä¿¡å·ä¸èƒ½å’Œ ALUSrc åˆå¹¶ï¼ï¼ï¼å› ä¸ºè¿™æ¶‰åŠåˆ°æœ‰çš„ I-type æŒ‡ä»¤éœ€è¦ç”¨åˆ°å¯„å­˜å™¨å †çš„è¯»portB (æ²¡é”™å°±æ˜¯ä½ `sw`)ï¼Œä½†æ˜¯ç”±äºè½¬å‘ï¼Œå®é™…ä¸Šåº”è¯¥è¯»çš„æ˜¯ ForwardB ä¿¡å·ç­›é€‰ä¹‹åçš„é‚£ä¸ªæ•°æ® final_b (ä½†æ˜¯è¿™ä¸ªä¿¡å·ä¸ä¼šè¿›ALUï¼Œè€Œæ˜¯ç«‹å³æ•°è¿›å»äº†) æ‰€ä»¥ï¼Œè¿™ä¸ªæ—¶å€™`sw`éœ€è¦å†™è¿›å»çš„æ•°æ®å°±è¢«å†²æ‰äº†â€¦â€¦

è§£å†³æ–¹æ¡ˆæ˜¯ç”¨ä¸¤ä¸ªMUXâ€¦ä¸€ä¸ªç”¨æ¥forwardç¡®å®šread_port_Bçš„æœ€ç»ˆæ­£ç¡®å€¼ï¼Œå¦å¤–ä¸€ä¸ªç”¨æ¥åœ¨è¿™ä¸ªè½¬å‘ç»“æœå’Œç«‹å³æ•°ç›´æ¥é€‰æ‹©ï¼Œç¡®å®šALU_Bçš„å€¼

##### å…³äºForward unit

å®ç°å¦‚ä¸‹ï¼šï¼ˆæ‘˜è‡ªCOD 5th Edition Ch 4.7ï¼‰

- EX *hazard*
	- `if (EX/MEM.RegWrite
		and (EX/MEM.RegisterRd =Ì¸ 0)
		 and (EX/MEM.RegisterRd = ID/EX.RegisterRs)) ForwardA = 10`
	- `if (EX/MEM.RegWrite
		and (EX/MEM.RegisterRd =Ì¸ 0)
		 and (EX/MEM.RegisterRd = ID/EX.RegisterRt)) ForwardB = 10`
- *MEM hazard:*
	- `if (MEM/WB.RegWrite
		and (MEM/WB.RegisterRd =Ì¸ 0)
		 and (MEM/WB.RegisterRd = ID/EX.RegisterRs)) ForwardA = 01`
	- `if (MEM/WB.RegWrite
		and (MEM/WB.RegisterRd =Ì¸ 0)
		 and (MEM/WB.RegisterRd = ID/EX.RegisterRt)) ForwardB = 01`
- MEM *hazard* (considering EX)
	- `if (MEM/WB.RegWrite
		and (MEM/WB.RegisterRd =Ì¸ 0)
		and not(EX/MEM.RegWrite and (EX/MEM.RegisterRd =Ì¸ 0)
			and (EX/MEM.RegisterRd =Ì¸ ID/EX.RegisterRs))
		and (MEM/WB.RegisterRd = ID/EX.RegisterRs)) ForwardA = 01`
	- `if (MEM/WB.RegWrite
		and (MEM/WB.RegisterRd =Ì¸ 0)
		and not(EX/MEM.RegWrite and (EX/MEM.RegisterRd =Ì¸ 0)
			and (EX/MEM.RegisterRd =Ì¸ ID/EX.RegisterRt))
		and (MEM/WB.RegisterRd = ID/EX.RegisterRt)) ForwardB = 01`

##### å…³äºå¯„å­˜å™¨å†…éƒ¨è½¬å‘

ç”±äºå¯„å­˜å™¨å †å†™æ˜¯éœ€è¦ä¸€ä¸ªæ—¶é’Ÿä¸Šå‡æ²¿çš„ï¼Œæ‰€ä»¥å®é™…ä¸Šå¯„å­˜å™¨å€¼çš„æ”¹å˜æ˜¯åœ¨WBæ®µä¹‹åï¼Œå¦‚å›¾æ‰€ç¤º

![Reg_available](pictures/Logically_available_C6.png)

æ‰€ä»¥ä¸ºäº†å¯„å­˜å™¨å†…éƒ¨æ•°æ®æ­£å¸¸å¯ç”¨ï¼Œåˆ™å¯ä»¥åœ¨å†™å…¥çš„æ—¶å€™å°±è½¬å‘åˆ°è¾“å‡ºç«¯å£å¤„ (æˆ‘é›†æˆåœ¨äº†reg_fileå†…éƒ¨)

### æ€è€ƒé¢˜

#### é™æ€åˆ†æ”¯é¢„æµ‹

å·²ç»åœ¨ä»£ç é‡Œé¢äº† (é»˜è®¤è·³è½¬å¤±è´¥çš„æ¨¡å¼)

#### åŠ¨æ€åˆ†æ”¯é¢„æµ‹

éœ€è¦ç”¨åˆ°ä¸€ä¸ªè®°å½•åˆ†æ”¯æŒ‡ä»¤çš„**åœ°å€**ï¼ˆæ˜¯**PC**å€¼ï¼Œä¸æ˜¯**è·³è½¬ç›®æ ‡**ï¼‰çš„æ•°ç»„ï¼ŒCOD5åŸæ–‡å¦‚ä¸‹ï¼š**branch prediction buffer** Also called **branch history table**. A small memory that is indexed by the lower portion of the address of the branch instruction and that contains one or more bits indicating whether the branch was recently taken or not.

è®°å½•PCçš„å·¥ä½œåœ¨ç¬¬ä¸€æ¬¡é‡åˆ°è¿™ä¸ªbeqçš„æ—¶å€™çš„IDæ®µ (å› ä¸ºç°åœ¨è¿˜ä¸çŸ¥é“è¿™ä¸ªæŒ‡ä»¤)ï¼Œä½†æ˜¯åç»­çš„æ¯ä¸€æ¬¡æ¯”è¾ƒéƒ½æ˜¯åœ¨IFæ®µï¼Œç„¶åé¢„æµ‹ä¸€ä¸‹æ˜¯å¦è·³è½¬ï¼Œæ›´æ–°PC (åƒé™æ€é¢„æµ‹ä¸€æ ·ä¼šæ›´æ–°ï¼Œåªæ˜¯è¯»è¿›å»çš„PCå—åˆ°é¢„æµ‹çš„å½±å“)ã€‚å½“è¿™ä¸€æ¡beqæ‰§è¡Œåˆ°IDæ®µæ—¶ï¼Œè‹¥åˆ¤æ–­ç»“æœä¸é¢„æµ‹ç»“æœä¸ç›¸ç¬¦ï¼Œåˆ™ç…§æ—§å¤„ç†

é¢„æµ‹é‡‡ç”¨ä¸¤ä½é¢„æµ‹ç çš„çŠ¶æ€æœºå®ç°ï¼š(å¦å¤–è®¾ä¸€ä¸ªUNDEFINEDçŠ¶æ€ï¼Œè¡¨ç¤ºè¿™æ¡æŒ‡ä»¤çš„PCè¿˜æ²¡æœ‰å‡ºç°è¿‡)

![FSM](pictures/FSM_for_prediction.png)

#### åŠ¨æ€åˆ†æ”¯é¢„æµ‹ä»£ç 

##### å®Œæ•´çš„CPUä»£ç 

åªæœ‰CPUæœ‰ä¿®æ”¹ï¼Œå¹¶æ·»åŠ äº†Predictå•å…ƒ

```verilog
module pipeline_CPU
	#(parameter WIDTH = 32)
	(
		input clk, rst
	);

	localparam [5:0] R_TYPE = 6'b000000;
	localparam [5:0] LW = 6'b100011;
	localparam [5:0] SW = 6'b101011;
	localparam [5:0] BEQ = 6'b000100;
	localparam [5:0] BNE = 6'b000101;
	localparam [5:0] J_TYPE = 6'b000010;
	localparam [5:0] ADDI = 6'b001000;

	localparam [2:0] TAKEN_DEEP = 3'b000;
	localparam [2:0] TAKEN_SHALLOW = 3'b001;
	localparam [2:0] NOT_SHALLOW = 3'b010;
	localparam [2:0] NOT_DEEP = 3'b011;
	localparam [2:0] UNDEFINED = 3'b100;

	reg [WIDTH-1:0] NPC, IR;					// IF/ID inter-stages register
	reg IF_TAKEN;								// IF/ID inter-stages register
	reg [WIDTH-1:0] A, B, IMM, RS, RT, RD;		// ID/EX inter-stages register
	// WAE for WB_address_from_EX_stage
	reg [WIDTH-1:0] ALUOut, WMD;				// EX/MEM inter-stages register
	reg [4:0] WAE;								// EX/MEM inter-stages register
	// MDR for mem_data_reg, ADR for ALUout_data_reg, WAM for WB_address_from_MEM_stage
	reg [WIDTH-1:0] MDR, ADR;					// MEM/WB inter-stages register
	reg [4:0] WAM;								// MEM/WB inter-stages register

	reg [WIDTH-1:0] br_pred_buf [0:7];			// branch prediction buffer, FIFO algorithm
	reg [2:0] FSM_call;							// to call which FSM
	reg [1:0] curr_FSM_cnt_BEQ, next_FSM_cnt_BEQ;		// copy new NPC of beq to which buffer
	reg [1:0] curr_FSM_cnt_BNE, next_FSM_cnt_BNE;		// copy new NPC of beq to which buffer
	reg [2:0] clr_sig;							// when the buffer is updated, the FSM state of that block should also be UNDEFINED
	reg [WIDTH-1:0] PC;

	wire [WIDTH-1:0] npc;						// PC+4, IF stage
	wire [WIDTH-1:0] ir;						// output of Instruction Mem, IF stage
	wire [WIDTH-1:0] real_npc;					// after branch prediction, IF stage
	wire [WIDTH-1:0] extend_addr;				// Sign extended addr (for beq), IF stage
	wire [WIDTH-1:0] pc_predict;				// BEQ addr, IF stage
	wire if_taken_0, if_taken_1, if_taken_2, if_taken_3;	// for BEQ FSM out, IF stage
	wire if_taken_4, if_taken_5, if_taken_6, if_taken_7;	// for BNE FSM out, IF stage
	wire [WIDTH-1:0] jump_addr;					// JUMP addr, ID stage
	wire [WIDTH-1:0] not_jump, PC_final;		// Branch complete, ID stage
	wire [9:0] haz;								// IR for Hazard Detection Unit, ID stage
	wire [5:0] op;								// IR for Control Unit, ID stage
	wire [WIDTH-1:0] a, b;						// register file read ports, ID stage
	wire [WIDTH-1:0] extend_imme;				// Sign extended imme, ID stage
	wire [WIDTH-1:0] shift_left, pc_beq;		// BEQ addr, IF stage
	wire [15:0] beq_forward;					// containing op, rs, rt infro, used in Forwarding Unit, ID stage
	wire [WIDTH-1:0] addr_imme;					// the same as 'extend_imme', EX stage
	wire [4:0] rs, rt, rd;						// ID fragments, EX stage
	wire [5:0] func;							// for ALU Control Unit, EX stage
	wire [WIDTH-1:0] alu_a, alu_b, alu_result;	// ALU input/output, EX stage
	wire ALU_Zero, cf, of, sf;					// for unused ports of ALU, EX stage
	wire [WIDTH-1:0] final_b;
	wire [4:0] rw_addr;							// chosen register write addr, EX stage
	wire [WIDTH-1:0] mem_out;					// MEM_IP out, MEM_stage
	wire [WIDTH-1:0] reg_data;					// WB stage
	wire [4:0] reg_addr;						// WB stage

	// Control Signals, definations & inter-stages
	reg [1:0] pred_curr_state, pred_next_state;	// for pridiction unit FSM, IF stage
	reg if_taken;								// if branch is taken, FSM, IF stage
	reg choose_0, choose_1, choose_2, choose_3;	// choose which FSM, will change condition
	reg clear_0, clear_1, clear_2, clear_3;		// clear which FSM, since it's been kicked out
	reg choose_4, choose_5, choose_6, choose_7;	// choose which FSM, will change condition
	reg clear_4, clear_5, clear_6, clear_7;		// clear which FSM, since it's been kicked out
	reg [1:0] ALUOp, ForwardA, ForwardB, ALU_MUX;
	reg [2:0] ALU_Ctrl, ForwardReg;
	reg PCwe;									// the Boss PC Write control, IF stage
	reg RegDst, AluSrc, MemRead, MemWrite, MemtoReg, RegWrite;
	reg Branch, Jump;
	reg FDWrite, ControlFlush, IFFlush;
	reg [1:0] ALUOp_DE;
	reg AluSrc_DE, RegDst_DE, MemRead_DE, MemWrite_DE, MemtoReg_DE, RegWrite_DE;
	reg MemRead_EM, MemWrite_EM, MemtoReg_EM, RegWrite_EM;
	reg MemtoReg_MW, RegWrite_MW;

	// Control Signals, in use, assigned from stage_regs
	wire Zero, PCSrc, PCSrc_predict_fail;

	assign op = IR[31:26];
	assign haz = IR[25:16];
	assign extend_imme = {((IR[15]) ? 16'hffff : 16'h0000), IR[15:0]};
	assign beq_forward = IR[31:16];
	assign rs = RS;
	assign rt = RT;
	assign rd = RD;
	assign addr_imme = IMM;
	assign reg_addr = WAM;

	// IF
	always @(posedge clk) begin
		if (rst) begin
			PC <= 0;
		end
		else if (PCwe) begin
			PC <= PC_final;
		end
		else
			PC <= PC;
	end

	assign npc = PC + 4;
	assign PCSrc_predict_fail = PCSrc ^ IF_TAKEN;

	mux MUX_BEQ (
		.m(PCSrc_predict_fail),
		.in_0(real_npc),
		.in_1(PCSrc_predict_fail ? (IF_TAKEN ? NPC : pc_beq) : real_npc),
		.out(not_jump)
	);

	mux MUX_JUMP (
		.m(Jump),
		.in_0(not_jump),
		.in_1(jump_addr),
		.out(PC_final)
	);

	dist_inst_rom instruction (
		.a(PC[9:2]),
		.spo(ir)
	);

	assign extend_addr = {((ir[15]) ? 16'hffff : 16'h0000), ir[15:0]};
	assign pc_predict = (extend_addr << 2) + npc;

	mux MUX_PREDICTION (
		.m(if_taken),
		.in_0(npc),
		.in_1(pc_predict),
		.out(real_npc)
	);

	always @(*) begin
		if (ir[31:26] == BEQ || npc == br_pred_buf[0]) begin
			FSM_call = 3'd0;
		end
		else if (ir[31:26] == BEQ || npc == br_pred_buf[1]) begin
			FSM_call = 3'd1;
		end
		else if (ir[31:26] == BEQ || npc == br_pred_buf[2]) begin
			FSM_call = 3'd2;
		end
		else if (ir[31:26] == BEQ || npc == br_pred_buf[3]) begin
			FSM_call = 3'd3;
		end
		else if (ir[31:26] == BNE || npc == br_pred_buf[4]) begin
			FSM_call = 3'd4;
		end
		else if (ir[31:26] == BNE || npc == br_pred_buf[5]) begin
			FSM_call = 3'd5;
		end
		else if (ir[31:26] == BNE || npc == br_pred_buf[6]) begin
			FSM_call = 3'd6;
		end
		else if (ir[31:26] == BNE || npc == br_pred_buf[7]) begin
			FSM_call = 3'd7;
		end
		else begin
			FSM_call = 'dz;
		end
	end

	always @(*) begin
		if_taken = 0;
		case (FSM_call)
			3'd0: begin
				if_taken = if_taken_0;
			end
			3'd1: begin
				if_taken = if_taken_1;
			end
			3'd2: begin
				if_taken = if_taken_2;
			end
			3'd3: begin
				if_taken = if_taken_3;
			end
			3'd4: begin
				if_taken = if_taken_4;
			end
			3'd5: begin
				if_taken = if_taken_5;
			end
			3'd6: begin
				if_taken = if_taken_6;
			end
			3'd7: begin
				if_taken = if_taken_7;
			end
			default: if_taken = 0;
		endcase
	end

	Prediction_BEQ prediction_0 (
		.clk(clk),
		.rst(rst),
		.PCSrc(PCSrc),
		.write_en(choose_0),
		.clear(clear_0),
		.if_taken(if_taken_0)
	);

	Prediction_BEQ prediction_1 (
		.clk(clk),
		.rst(rst),
		.PCSrc(PCSrc),
		.write_en(choose_1),
		.clear(clear_1),
		.if_taken(if_taken_1)
	);

	Prediction_BEQ prediction_2 (
		.clk(clk),
		.rst(rst),
		.PCSrc(PCSrc),
		.write_en(choose_2),
		.clear(clear_2),
		.if_taken(if_taken_2)
	);

	Prediction_BEQ prediction_3 (
		.clk(clk),
		.rst(rst),
		.PCSrc(PCSrc),
		.write_en(choose_3),
		.clear(clear_3),
		.if_taken(if_taken_3)
	);

	Prediction_BNE prediction_4 (
		.clk(clk),
		.rst(rst),
		.PCSrc(PCSrc),
		.write_en(choose_4),
		.clear(clear_4),
		.if_taken(if_taken_4)
	);

	Prediction_BNE prediction_5 (
		.clk(clk),
		.rst(rst),
		.PCSrc(PCSrc),
		.write_en(choose_5),
		.clear(clear_5),
		.if_taken(if_taken_5)
	);

	Prediction_BNE prediction_6 (
		.clk(clk),
		.rst(rst),
		.PCSrc(PCSrc),
		.write_en(choose_6),
		.clear(clear_6),
		.if_taken(if_taken_6)
	);

	Prediction_BNE prediction_7 (
		.clk(clk),
		.rst(rst),
		.PCSrc(PCSrc),
		.write_en(choose_7),
		.clear(clear_7),
		.if_taken(if_taken_7)
	);

	// IF/ID inter-stages registers
	always @(posedge clk) begin
		if (IFFlush || rst) begin
			NPC <= 0;
			IR <= 0;
			IF_TAKEN <= 0;
		end
		else begin
			if (FDWrite) begin
				NPC <= npc;
				IR <= ir;
				IF_TAKEN <= if_taken;
			end
			else begin
				NPC <= NPC;
				IR <= IR;
				IF_TAKEN <= IF_TAKEN;
			end
		end
	end

	// ID
	reg_file registers (
		.clk(clk),
		.forward(ForwardReg),
		.ex(alu_result),
		.mem(mem_out),
		.ra0(IR[25:21]),
		.ra1(IR[20:16]),
		.rd0(a),
		.rd1(b),
		.wa(reg_addr),
		.wd(reg_data),
		.we(RegWrite_MW)
	);

	// for BEQ, BNE and Jump
	assign Zero = (a == b) ? 1'b1 : 1'b0;
	assign shift_left = extend_imme << 2;
	assign pc_beq = shift_left + NPC;
	assign jump_addr = {NPC[31:28], IR[25:0], 2'b00};

	// Prediction Unit
	// Record NPC for easier
	initial begin
		br_pred_buf[0] = 'dz;
		br_pred_buf[1] = 'dz;
		br_pred_buf[2] = 'dz;
		br_pred_buf[3] = 'dz;
		br_pred_buf[4] = 'dz;
		br_pred_buf[5] = 'dz;
		br_pred_buf[6] = 'dz;
		br_pred_buf[7] = 'dz;
		curr_FSM_cnt_BEQ = 2'd0;
		next_FSM_cnt_BEQ = 2'd0;
		curr_FSM_cnt_BNE = 2'd0;
		next_FSM_cnt_BNE = 2'd0;
	end

	always @(*) begin
		{choose_0, choose_1, choose_2, choose_3, choose_4, choose_5, choose_6, choose_7} = 0;
		if (op == BEQ) begin
			if (NPC != br_pred_buf[0] && NPC != br_pred_buf[1] &&
				NPC != br_pred_buf[2] && NPC != br_pred_buf[3])
			// new beq's PC+4 
			begin
				br_pred_buf[curr_FSM_cnt_BEQ] = NPC;
				next_FSM_cnt_BEQ = curr_FSM_cnt_BEQ + 1;
				clr_sig = curr_FSM_cnt_BEQ;
			end
			else if (NPC == br_pred_buf[0]) begin
				next_FSM_cnt_BEQ = next_FSM_cnt_BEQ;
				clr_sig = 'dz;
				choose_0 = 1;
			end
			else if (NPC == br_pred_buf[1]) begin
				next_FSM_cnt_BEQ = next_FSM_cnt_BEQ;
				clr_sig = 'dz;
				choose_1 = 1;
			end
			else if (NPC == br_pred_buf[2]) begin
				next_FSM_cnt_BEQ = next_FSM_cnt_BEQ;
				clr_sig = 'dz;
				choose_2 = 1;
			end
			else if (NPC == br_pred_buf[3]) begin
				next_FSM_cnt_BEQ = next_FSM_cnt_BEQ;
				clr_sig = 'dz;
				choose_3 = 1;
			end
		end
		else if (op == BNE) begin
			if (NPC != br_pred_buf[4] && NPC != br_pred_buf[5] &&
				NPC != br_pred_buf[6] && NPC != br_pred_buf[7])
			// new beq's PC+4 
			begin
				br_pred_buf[curr_FSM_cnt_BNE + 4] = NPC;
				next_FSM_cnt_BNE = curr_FSM_cnt_BNE + 1;
				clr_sig = curr_FSM_cnt_BNE + 4;
			end
			else if (NPC == br_pred_buf[4]) begin
				next_FSM_cnt_BNE = next_FSM_cnt_BNE;
				clr_sig = 'dz;
				choose_4 = 1;
			end
			else if (NPC == br_pred_buf[5]) begin
				next_FSM_cnt_BNE = next_FSM_cnt_BNE;
				clr_sig = 'dz;
				choose_5 = 1;
			end
			else if (NPC == br_pred_buf[6]) begin
				next_FSM_cnt_BNE = next_FSM_cnt_BNE;
				clr_sig = 'dz;
				choose_6 = 1;
			end
			else if (NPC == br_pred_buf[7]) begin
				next_FSM_cnt_BNE = next_FSM_cnt_BNE;
				clr_sig = 'dz;
				choose_7 = 1;
			end
		end
		else begin
			// not branch instruction, no need for prediction
			br_pred_buf[0] = br_pred_buf[0];
			br_pred_buf[1] = br_pred_buf[1];
			br_pred_buf[2] = br_pred_buf[2];
			br_pred_buf[3] = br_pred_buf[3];
			br_pred_buf[4] = br_pred_buf[4];
			br_pred_buf[5] = br_pred_buf[5];
			br_pred_buf[6] = br_pred_buf[6];
			br_pred_buf[7] = br_pred_buf[7];
			next_FSM_cnt_BEQ = next_FSM_cnt_BEQ;
			next_FSM_cnt_BNE = next_FSM_cnt_BNE;
			clr_sig = 'dz;
		end
	end

	always @(posedge clk) begin
		curr_FSM_cnt_BEQ <= next_FSM_cnt_BEQ;
		curr_FSM_cnt_BNE <= next_FSM_cnt_BNE;
	end

	always @(*) begin
		{clear_0, clear_1, clear_2, clear_3, clear_4, clear_5, clear_6, clear_7} = 0;
		case (clr_sig)
			3'd0: clear_0 = 1;
			3'd1: clear_1 = 1;
			3'd2: clear_2 = 1;
			3'd3: clear_3 = 1;
			default: {clear_0, clear_1, clear_2, clear_3, clear_4, clear_5, clear_6, clear_7} = 0;
		endcase
	end

	// Control Unit
	always @(posedge clk) begin
		if (rst) begin
			{RegDst, Jump, Branch, MemRead, MemtoReg, RegWrite, MemWrite, ALUOp, IFFlush, AluSrc, ForwardReg} = 0;
		end
	end

	always @(*) begin
		{RegDst, Jump, Branch, MemRead, MemtoReg, RegWrite, MemWrite, ALUOp, AluSrc} = 0;
		case (op)
			6'b000000: begin					// R-type
				RegDst = 1'b1;
				RegWrite = 1'b1;
				ALUOp = 2'b10;
			end
			6'b100011: begin					// lw
				AluSrc = 1'b1;
				MemtoReg = 1'b1;
				RegWrite = 1'b1;
				MemRead = 1'b1;
			end
			6'b101011: begin					// sw
				AluSrc = 1'b1;
				MemWrite = 1'b1;
			end
			6'b000100: begin					// beq
				Branch = 1'b1;
				ALUOp = 2'b01;
			end
			6'b000101: begin					// bne
				Branch = 1'b1;
				ALUOp = 2'b01;
			end
			6'b001000: begin					// addi
				AluSrc = 1'b1;
				RegWrite = 1'b1;
			end
			6'b000010: begin					// Jump
				Jump = 1'b1;
			end
			default: {RegDst, Jump, Branch, MemRead, MemtoReg, RegWrite, MemWrite, ALUOp, AluSrc} = 'dz;
		endcase
	end

	// Hazard Detection Unit
	// consider R-type, BEQ, BNE, SW at ID
	// Priority considered
	assign PCSrc = (op == BNE) ? (~Zero & Branch) : (Zero & Branch);
	always @(*) begin
		// no need to stall when BEQ is right after a non-mem-visiting instruction,
		// since forwarding from EX result to ID is done (might decrease clk rate)
		if (((op == BEQ) || (op == BEQ)) && 
			(MemRead_DE && ((rt == haz[4:0]) || (rt == haz[9:5])))) begin
			// LW + BEQ/BNE hazard, BEQ/BNE at ID, stall 1 cycle
			PCwe = 0;
			IFFlush = 0;
			FDWrite = 0;
			ControlFlush = 1;
		end
		// Jump success or branch prediction fail, clear PC and IF/ID inter-stages registers
		else if (Jump || PCSrc_predict_fail) begin
			PCwe = 1;
			IFFlush = 1;
			FDWrite = 0;						// low priority, 0 or 1 both are ok
			ControlFlush = 0;					// since BEQ/JUMP do not have latter stages, 0 or 1 both are ok
		end
		// R-type or SW need to be considered since they need rt as operation source
		else if (MemRead_DE && (((rt == haz[4:0]) && ((op == 6'b00000) || (op == 6'b101011))) || (rt == haz[9:5]))) begin
			PCwe = 0;
			IFFlush = 0;
			FDWrite = 0;
			ControlFlush = 1;
		end
		else begin
			PCwe = 1;
			IFFlush = 0;
			FDWrite = 1;
			ControlFlush = 0;
		end
	end

	// ID/EX inter-stages registers
	// in order to avoid possible data impact, I put all data to 'dz if this reg_pile is set clear
	always @(posedge clk) begin
		if (~IFFlush && ~ControlFlush) begin
			A <= a;
			B <= b;
			RS <= IR[25:21];
			RT <= IR[20:16];
			RD <= IR[15:11];
			RegDst_DE <= RegDst;
			ALUOp_DE <= ALUOp;
			MemRead_DE <= MemRead;
			MemtoReg_DE <= MemtoReg;
			MemWrite_DE <= MemWrite;
			RegWrite_DE <= RegWrite;
			AluSrc_DE <= AluSrc;
			IMM <= extend_imme;
		end
		else begin
			{A, B, RS, RT, RD, IMM} <= 'dz;
			{RegDst_DE, MemRead_DE, MemtoReg_DE, RegWrite_DE, MemWrite_DE, ALUOp_DE, AluSrc_DE} <= 0;
		end
	end

	// EX
	multi_mux MUX_ALU_A (
		.n(2'd2),
		.m(ForwardA),
		.in_0(A),
		.in_1(reg_data),
		.in_2(ALUOut),
		.out(alu_a)
	);

	// AMB Unit (AMB for ALU_MUX_B)
	// always @(*) begin
	// 	if (AluSrc_DE) begin
	// 		ALU_MUX = 2'd3;
	// 	end
	// 	else begin
	// 		ALU_MUX = ForwardB;
	// 	end
	// end

	// multi_mux MUX_ALU_B (
	// 	.n(2'd3),
	// 	.m(ALU_MUX),
	// 	.in_0(B),
	// 	.in_1(reg_data),
	// 	.in_2(ALUOut),
	// 	.in_3(addr_imme),
	// 	.out(alu_b)
	// );

	multi_mux MUX_FINAL_B (
		.n(2'd2),
		.m(ForwardB),
		.in_0(B),
		.in_1(reg_data),
		.in_2(ALUOut),
		.out(final_b)
	);

	assign addr_imme = IMM;
	assign func = IMM[5:0];

	mux MUX_ALU_B (
		.m(AluSrc_DE),
		.in_0(final_b),
		.in_1(IMM),
		.out(alu_b)
	);

	// ALU Control Unit
	always @(*) begin
		case (ALUOp_DE)
			2'b00: begin				// LW & SW
				ALU_Ctrl = 3'b010;
			end
			2'b01: begin				// BEQ
				ALU_Ctrl = 3'b110;
			end
			2'b10: begin				// R-type
				case (func)
					6'b100000: begin	// add
						ALU_Ctrl = 3'b010;
					end
					6'b100010: begin	// sub
						ALU_Ctrl = 3'b110;
					end
					6'b100100: begin	// and
						ALU_Ctrl = 3'b000;
					end
					6'b100101: begin	// or
						ALU_Ctrl = 3'b001;
					end
					6'b101010: begin	// slt
						ALU_Ctrl = 3'b111;
					end
					default: ALU_Ctrl = 'dz;
				endcase
			end
			default: ALU_Ctrl = 'dz;
		endcase
	end

	ALU alu (
		.y(alu_result),
		.zf(ALU_Zero),
		.cf(cf),
		.of(of),
		.sf(sf),
		.a(alu_a),
		.b(alu_b),
		.m(ALU_Ctrl)
	);

	mux MUX_REGDST (
		.m(RegDst_DE),
		.in_0(rt),
		.in_1(rd),
		.out(rw_addr)
	);

	// Forwarding Unit
	// NOT solved WB-start to MEM-start
	// solved WB-start or MEM-start to EX-start
	// For ForwardReg: MEM-start (EX result) or MEM-end (MEM result) to ID
	// 000 for non-forwarding, 001 from ALU to A, 010 from ALU to B,
	// 011 from MEM to A, 100 from MEM to B
	always @(*) begin
		// forward to EX-start
		if (RegWrite_EM && (|WAE) && (WAE == rs)) begin
			ForwardA = 2'b10;			
		end
		else if (RegWrite_MW && (|WAM) && (WAM == rs)) begin
			ForwardA = 2'b01;
		end
		else begin
			ForwardA = 2'b00;
		end
	end

	always @(*) begin
		// forward to EX-start
		if (RegWrite_EM && (|WAE) && (WAE == rt)) begin
			ForwardB = 2'b10;
		end
		else if (RegWrite_MW && (|WAM) && (WAM == rt)) begin
			ForwardB = 2'b01;
		end
		else begin
			ForwardB = 2'b00;
		end
	end

	always @(*) begin
		// current instruction in ID is a BEQ
		if (beq_forward[15:10] == 6'b000100) begin
			if (RegWrite_DE && (|rw_addr) && (rw_addr == beq_forward[9:5])) begin
				ForwardReg = 3'b001;
			end
			else if (RegWrite_DE && (|rw_addr) && (rw_addr == beq_forward[4:0])) begin
				ForwardReg = 3'b010;
			end
			else if (RegWrite_EM && (|WAE) && (WAE == beq_forward[9:5])) begin
				ForwardReg = 3'b011;
			end
			else if (RegWrite_EM && (|WAE) && (WAE == beq_forward[4:0])) begin
				ForwardReg = 3'b100;
			end
			else begin
				ForwardReg = 3'b000;
			end
		end
		else begin
			ForwardReg = 3'b000;
		end
	end

	// EX/MEM inter-stages registers
	always @(posedge clk) begin
		ALUOut <= alu_result;
		WMD <= final_b;
		WAE <= rw_addr;
		MemRead_EM <= MemRead_DE;
		MemWrite_EM <= MemWrite_DE;
		MemtoReg_EM <= MemtoReg_DE;
		RegWrite_EM <= RegWrite_DE;
	end

	// MEM
	dist_data_ram memory (
		.a(ALUOut[9:2]),
		.d(WMD),
		.clk(clk),
		.we(MemWrite_EM),
		.spo(mem_out)
	);

	// MEM/WB inter-stages registers
	always @(posedge clk) begin
		MDR <= mem_out;
		ADR <= ALUOut;
		WAM <= WAE;
		MemtoReg_MW <= MemtoReg_EM;
		RegWrite_MW <= RegWrite_EM;
	end

	// WB
	mux MUX_WB (
		.m(MemtoReg_MW),
		.in_0(ADR),
		.in_1(MDR),
		.out(reg_data)
	);
endmodule
```

##### é¢„æµ‹å•å…ƒ

```verilog
module Prediction_BEQ
        #(parameter WIDTH = 32)
    (
        input clk, rst,
        input PCSrc,
        input write_en,
        input clear,
        output reg if_taken
    );

	localparam [5:0] R_TYPE = 6'b000000;
	localparam [5:0] LW = 6'b100011;
	localparam [5:0] SW = 6'b101011;
	localparam [5:0] BEQ = 6'b000100;
	localparam [5:0] BNE = 6'b000101;
	localparam [5:0] J_TYPE = 6'b000010;
	localparam [5:0] ADDI = 6'b001000;

	localparam [2:0] TAKEN_DEEP = 3'b000;
	localparam [2:0] TAKEN_SHALLOW = 3'b001;
	localparam [2:0] NOT_SHALLOW = 3'b010;
	localparam [2:0] NOT_DEEP = 3'b011;
	localparam [2:0] UNDEFINED = 3'b100;

	reg [1:0] pred_curr_state, pred_next_state;	// for pridiction unit FSM, IF stage

	always @(posedge clk) begin
		if (rst || clear) begin
			pred_curr_state <= UNDEFINED;
		end
		else if (write_en) begin
			pred_curr_state <= pred_next_state;
		end
		else begin
			pred_curr_state <= pred_curr_state;
		end
	end

	always @(*) begin
		case (pred_curr_state)
			UNDEFINED: pred_next_state = PCSrc ? TAKEN_SHALLOW : NOT_SHALLOW;
			TAKEN_DEEP: pred_next_state = PCSrc ? TAKEN_DEEP : TAKEN_SHALLOW;
			TAKEN_SHALLOW: pred_next_state = PCSrc ? TAKEN_DEEP : NOT_SHALLOW;
			NOT_SHALLOW: pred_next_state = PCSrc ? TAKEN_SHALLOW : NOT_DEEP;
			NOT_DEEP: pred_next_state = PCSrc ? NOT_SHALLOW : NOT_DEEP;
			default: pred_next_state = UNDEFINED;
		endcase
	end

	// This controls the change of if_taken, though this signal is being used in IF stage
	always @(*) begin
		if_taken = 0;
		case (pred_curr_state)
			UNDEFINED: if_taken = 1;
			TAKEN_DEEP: if_taken = 1;
			TAKEN_SHALLOW: if_taken = 1;
			NOT_SHALLOW: if_taken = 0;
			NOT_DEEP: if_taken = 0;
			default: if_taken = 0;
		endcase
	end
endmodule

module Prediction_BNE
        #(parameter WIDTH = 32)
    (
        input clk, rst,
        input PCSrc,
        input write_en,
        input clear,
        output reg if_taken
    );

	localparam [5:0] R_TYPE = 6'b000000;
	localparam [5:0] LW = 6'b100011;
	localparam [5:0] SW = 6'b101011;
	localparam [5:0] BEQ = 6'b000100;
	localparam [5:0] BNE = 6'b000101;
	localparam [5:0] J_TYPE = 6'b000010;
	localparam [5:0] ADDI = 6'b001000;

	localparam [2:0] TAKEN_DEEP = 3'b000;
	localparam [2:0] TAKEN_SHALLOW = 3'b001;
	localparam [2:0] NOT_SHALLOW = 3'b010;
	localparam [2:0] NOT_DEEP = 3'b011;
	localparam [2:0] UNDEFINED = 3'b100;

	reg [1:0] pred_curr_state, pred_next_state;	// for pridiction unit FSM, IF stage

	always @(posedge clk) begin
		if (rst || clear) begin
			pred_curr_state <= UNDEFINED;
		end
		else if (write_en) begin
			pred_curr_state <= pred_next_state;
		end
		else begin
			pred_curr_state <= pred_curr_state;
		end
	end


	always @(*) begin
		case (pred_curr_state)
			UNDEFINED: pred_next_state = PCSrc ? TAKEN_SHALLOW : NOT_SHALLOW;
			TAKEN_DEEP: pred_next_state = PCSrc ? TAKEN_DEEP : TAKEN_SHALLOW;
			TAKEN_SHALLOW: pred_next_state = PCSrc ? TAKEN_DEEP : NOT_SHALLOW;
			NOT_SHALLOW: pred_next_state = PCSrc ? TAKEN_SHALLOW : NOT_DEEP;
			NOT_DEEP: pred_next_state = PCSrc ? NOT_SHALLOW : NOT_DEEP;
			default: pred_next_state = UNDEFINED;
		endcase
	end

	// This controls the change of if_taken, though this signal is being used in IF stage
	always @(*) begin
		if_taken = 0;
		case (pred_curr_state)
			UNDEFINED: if_taken = 0;
			TAKEN_DEEP: if_taken = 1;
			TAKEN_SHALLOW: if_taken = 1;
			NOT_SHALLOW: if_taken = 0;
			NOT_DEEP: if_taken = 0;
			default: if_taken = 0;
		endcase
	end
endmodule
```

##### ä¸»è¦çš„ä»£ç åˆ†æ

æ‰‹å†™çš„åˆ†æï¼šå·¦ä¸Šè§’æ˜¯IFæ®µæ®µæ•°æ®é€šè·¯ï¼Œä¸­é—´æ˜¯å¯¹é¢„æµ‹æ§åˆ¶çš„è¯¦ç»†è§£é‡Š (æ€æƒ³æ¥è‡ªCOD5)ï¼Œå·¦ä¸‹è§’æ˜¯å¯¹æŒ‡ä»¤5é˜¶æ®µé™æ€ä¸åŠ¨æ€çš„æ¯”è¾ƒ

![prediction](pictures/pipeline_CPU_prediction.png)

#### ä¸é™æ€åˆ†æ”¯é¢„æµ‹çš„æ¯”è¾ƒ

##### ä»¿çœŸä»£ç 

```verilog
module tb_CPU();
    reg clk, rst;

    pipeline_CPU cpu (
        .clk(clk),
        .rst(rst)
    );

    initial
    begin
        clk = 1;
        rst = 1;
        # 4 rst = 0;
        # 996 $finish;
    end

    always
    # 2 clk = ~clk;
endmodule
```

##### æµ‹è¯•æ–‡ä»¶ (.asm)

```assembly
_test0:
	add $t0, $0, $0			# $t0 = 0		0
	addi $t1, $0, 100		# $t1 = 100		4
	j _test1			#			8

_test1:
	addi $t0, $t0, 1		# $t0++			12
	bne $t0, $t1, _test1		#			16

_success:
	j _success			#			20
```

##### æµ‹è¯•æ–‡ä»¶ (.coe)

```plain text
memory_initialization_radix  = 16;
memory_initialization_vector =
00004020
20090064
08000003
21080001
1509fffe
08000005
```

##### æµ‹è¯•å¯¹æ¯”

###### åŠ¨æ€åˆ†æ”¯é¢„æµ‹

![dynamic](pictures/dynamic_prediction.png)

å¯è§ï¼ŒåŠ¨æ€åˆ†æ”¯é¢„æµ‹åœ¨æŒç»­çš„å¾ªç¯é¢å‰ä¼˜åŒ–æ•ˆç‡å¾ˆé«˜ï¼ˆå› ä¸ºæ¯ä¸€æ¬¡é¢„æµ‹éƒ½æ˜¯æˆåŠŸçš„ï¼‰åªæœ‰å¾ªç¯å†…éƒ¨çš„ä»£ç äº¤æ›¿æ‰§è¡Œï¼Œæ²¡æœ‰å»¶è¿Ÿæ§½

###### é™æ€åˆ†æ”¯é¢„æµ‹

![static](pictures/static_prediction.png)

é™æ€åˆ†æ”¯é¢„æµ‹æ˜¯2nsä¸€ä¸ªæ—¶é’Ÿå‘¨æœŸçš„ï¼Œè€ŒåŠ¨æ€åˆ†æ”¯é¢„æµ‹æ˜¯4nsä¸€ä¸ªæ—¶é’Ÿå‘¨æœŸï¼Œæ•ˆç‡ä¼˜åŒ–å¯ä»¥å¾ˆæ¸…æ™°çš„çœ‹å‡º